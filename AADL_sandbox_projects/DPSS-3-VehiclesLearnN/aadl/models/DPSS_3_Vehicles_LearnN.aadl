package DPSS_3_Vehicles_LearnN_Pkg
public
	with DPSS_Data_Types;
	with DPSS_Node_Lib;
	with DPSS_Constants;
	with UAV_Pkg;
	
	system DPSS_3_Vehicles_LearnN
		features
			-----------------------------------------------------
			--INPUTS
			-----------------------------------------------------
			
			--Initial positions
			initial_pos_UAV1 : in data port DPSS_Data_Types::Position_Type;
			initial_pos_UAV2 : in data port DPSS_Data_Types::Position_Type;
			initial_pos_UAV3 : in data port DPSS_Data_Types::Position_Type;
						
			--Suggested initial directions
			suggested_initial_direction_UAV1 : in data port DPSS_Data_Types::Direction_Type;
			suggested_initial_direction_UAV2 : in data port DPSS_Data_Types::Direction_Type;
			suggested_initial_direction_UAV3 : in data port DPSS_Data_Types::Direction_Type;
			
			-----------------------------------------------------
			--OUTPUTS
			-----------------------------------------------------

			pos_UAV1 : out data port DPSS_Data_Types::Position_Type;
			pos_UAV2 : out data port DPSS_Data_Types::Position_Type;
			pos_UAV3 : out data port DPSS_Data_Types::Position_Type;
			 
		annex agree {**
			
			-----------------------------------------------------
			--CONSTANTS
			-----------------------------------------------------
			
			--Constants that the vehicles don't know a priori
			const N_int : int = 3; --Number of vehicles
			const N_real : real = 3.0; --Number of vehicles

			-----------------------------------------------------
			--EQ (STATE) VARIABLES
			-----------------------------------------------------
			
			--Directions						 
			eq direction_UAV1 : DPSS_Data_Types::Direction_Type;
			eq direction_UAV2 : DPSS_Data_Types::Direction_Type;
			eq direction_UAV3 : DPSS_Data_Types::Direction_Type;
	
			--Goals
			eq goal_UAV1 : DPSS_Data_Types::Position_Type;
			eq goal_UAV2 : DPSS_Data_Types::Position_Type;
			eq goal_UAV3 : DPSS_Data_Types::Position_Type;
			
			--Shared border positions
			eq S_12 : DPSS_Data_Types::Position_Type = 1.0*DPSS_Constants.P_GLOBAL/N_real;  
			eq S_23 : DPSS_Data_Types::Position_Type = 2.0*DPSS_Constants.P_GLOBAL/N_real;
			
			--Previous positions
			eq pre_pos_UAV1 : DPSS_Data_Types::Position_Type = prev(pos_UAV1, initial_pos_UAV1);
			eq pre_pos_UAV2 : DPSS_Data_Types::Position_Type = prev(pos_UAV2, initial_pos_UAV2);
			eq pre_pos_UAV3 : DPSS_Data_Types::Position_Type = prev(pos_UAV3, initial_pos_UAV3);
			
			--Previous directions
			eq pre_direction_UAV1 : DPSS_Data_Types::Direction_Type = prev(direction_UAV1, suggested_initial_direction_UAV1);
			eq pre_direction_UAV2 : DPSS_Data_Types::Direction_Type = prev(direction_UAV2, suggested_initial_direction_UAV2);
			eq pre_direction_UAV3 : DPSS_Data_Types::Direction_Type = prev(direction_UAV3, suggested_initial_direction_UAV3);
			
			--Previous goals
			eq pre_goal_UAV1 : DPSS_Data_Types::Position_Type = prev(goal_UAV1, (if direction_UAV1 = 1 then DPSS_Constants.P_GLOBAL else 0.0));
			eq pre_goal_UAV2 : DPSS_Data_Types::Position_Type = prev(goal_UAV2, (if direction_UAV2 = 1 then DPSS_Constants.P_GLOBAL else 0.0));
			eq pre_goal_UAV3 : DPSS_Data_Types::Position_Type = prev(goal_UAV3, (if direction_UAV3 = 1 then DPSS_Constants.P_GLOBAL else 0.0));
									
			--Times to reach neighbors													
			eq time_to_reach_neighbor_12 : DPSS_Data_Types::Time_Type = 
				DPSS_Node_Lib.time_to_reach_neighbor(pre_direction_UAV1,
														pre_direction_UAV2,
														pre_pos_UAV1,
														pre_pos_UAV2
														);
																		
			eq time_to_reach_neighbor_23 : DPSS_Data_Types::Time_Type = 
				DPSS_Node_Lib.time_to_reach_neighbor(pre_direction_UAV2,
														pre_direction_UAV3,
														pre_pos_UAV2,
														pre_pos_UAV3
														);
			
			--Times to reach goals
			eq time_until_UAV1_reaches_goal : DPSS_Data_Types::Time_Type = 
				DPSS_Node_Lib.time_to_reach_target_position(
					pre_direction_UAV1, 
					pre_pos_UAV1, 
					pre_goal_UAV1
				);
				
			eq time_until_UAV2_reaches_goal : DPSS_Data_Types::Time_Type = 
				DPSS_Node_Lib.time_to_reach_target_position(
					pre_direction_UAV2, 
					pre_pos_UAV2, 
					pre_goal_UAV2
				);
				
			eq time_until_UAV3_reaches_goal : DPSS_Data_Types::Time_Type = 
				DPSS_Node_Lib.time_to_reach_target_position(
					pre_direction_UAV3, 
					pre_pos_UAV3, 
					pre_goal_UAV3
				);
			
			eq time_until_UAV3_reaches_P : DPSS_Data_Types::Time_Type = 
				DPSS_Node_Lib.time_to_reach_target_position(
					pre_direction_UAV3, 
					pre_pos_UAV3, 
					DPSS_Constants.P_GLOBAL
				);
						
			eq deltaT : DPSS_Data_Types::Time_Type =
							--Added time_until_UAV3_reaches_P since UAV3 might not know where P is.
							DPSS_Node_Lib.min_pos_time(time_until_UAV3_reaches_P,
							DPSS_Node_Lib.min_pos_time(time_until_UAV1_reaches_goal,
							DPSS_Node_Lib.min_pos_time(time_until_UAV2_reaches_goal,
							DPSS_Node_Lib.min_pos_time(time_until_UAV3_reaches_goal,
							DPSS_Node_Lib.min_pos_time(time_to_reach_neighbor_12,
												   time_to_reach_neighbor_23)))));
			
			eq stable : bool = 
				--Either
				(	--the UAVs are at 0, S_23, and S_23, respectively
					(
						pos_UAV1 = 0.0 and 
						pos_UAV2 = S_23 and 
						pos_UAV3 = S_23 
					)
					--and were previously at S_12, S_12, and P, respectively
					and (
						pre_pos_UAV1 = S_12 and 
						pre_pos_UAV2 = S_12 and 
						pre_pos_UAV3 = DPSS_Constants.P_GLOBAL
					)
				)
				--or
				or (--the UAVs are at S_12, S_12, and P, respectively
					(
						pos_UAV1 = S_12 and 
						pos_UAV2 = S_12 and 
						pos_UAV3 = DPSS_Constants.P_GLOBAL
					)
					--and were previously at 0, S_23, and S_23, respectively
					and (
						pre_pos_UAV1 = 0.0 and 
						pre_pos_UAV2 = S_23 and 
						pre_pos_UAV3 = S_23 
					)
				);
						
			-----------------------------------------------------
			--ASSUMPTIONS
			-----------------------------------------------------
			
			assume "time update": time = (0.0 -> pre(time) + deltaT);
			
			assume "UAVs are numbered according to their position from left to right":
				(	initial_pos_UAV1 <= initial_pos_UAV2
				and initial_pos_UAV2 <= initial_pos_UAV3)
				-> true;
				
			--We didn't need this when the vehicles know P, but we
			-- need it when they don't know P. Otherwise, if initially co-located at 0,
			-- UAV1 needs to turn around at epsilon and we don't have a good way to model that.
			assume "UAVs are not co-located on the initial timestep":
				(	initial_pos_UAV1 < initial_pos_UAV2 
				and initial_pos_UAV2 < initial_pos_UAV3)
				-> true;
				
			assume "Initial positions are between 0 and P_GLOBAL":
				(--Initial positions are >= 0
					0.0 <= initial_pos_UAV1 
				and 0.0 <= initial_pos_UAV2
				and 0.0 <= initial_pos_UAV3
				--and initial positions are <= P
				and initial_pos_UAV1 <= DPSS_Constants.P_GLOBAL
 				and initial_pos_UAV2 <= DPSS_Constants.P_GLOBAL
 				and initial_pos_UAV3 <= DPSS_Constants.P_GLOBAL
				)
				-> true;
				
			assume "Suggested initial directions are LEFT or RIGHT":
				((			suggested_initial_direction_UAV1 = DPSS_Constants.LEFT
						or  suggested_initial_direction_UAV1 = DPSS_Constants.RIGHT)
					and (	suggested_initial_direction_UAV2 = DPSS_Constants.LEFT
						or 	suggested_initial_direction_UAV2 = DPSS_Constants.RIGHT)
					and (	suggested_initial_direction_UAV3 = DPSS_Constants.LEFT
						or 	suggested_initial_direction_UAV3 = DPSS_Constants.RIGHT)
				)
				-> true;

			-----------------------------------------------------
			--GUARANTEES
			-----------------------------------------------------
			
			-- Main Theorem			
			guarantee "Theorem 2 for Algorithm B for 3 Vehicles":
				(time >= ((5.0*N_real - 1.0)/N_real)*DPSS_Constants.Time_to_travel_full_length) =>
					(stable	and deltaT = DPSS_Constants.Time_to_travel_full_length/N_real);
			
			guarantee "deltaT is positive":
				true -> deltaT > 0.0;
			
			guarantee "Initial positions are the input initial positions":
				(	pos_UAV1 = initial_pos_UAV1
				and pos_UAV2 = initial_pos_UAV2
				and pos_UAV3 = initial_pos_UAV3) -> true;
			
			guarantee "Goals are in bounds":
				--Goals are >= 0
					0.0 <= goal_UAV1
				and 0.0 <= goal_UAV2
				and 0.0 <= goal_UAV3
				--and goals are <=P
				and goal_UAV1 <= DPSS_Constants.P_GLOBAL
				and goal_UAV2 <= DPSS_Constants.P_GLOBAL
				and goal_UAV3 <= DPSS_Constants.P_GLOBAL;	
			
			guarantee "Positions are in bounds":
				--Positions are >= 0
					0.0 <= pos_UAV1
				and 0.0 <= pos_UAV2	 
				and 0.0 <= pos_UAV3
				--and positions are <= P
				and pos_UAV1 <= DPSS_Constants.P_GLOBAL
				and pos_UAV2 <= DPSS_Constants.P_GLOBAL
				and pos_UAV3 <= DPSS_Constants.P_GLOBAL;
			
			guarantee "UAVs stay in order":
					pos_UAV1 <= pos_UAV2
				and pos_UAV2 <= pos_UAV3;
			
			guarantee "UAVs turn around at the borders":
				--UAVs turn around at 0
					pos_UAV1 = 0.0 => direction_UAV1 = 1
				and pos_UAV2 = 0.0 => direction_UAV2 = 1
				and pos_UAV3 = 0.0 => direction_UAV3 = 1
				--and UAVs turn around at P
				and pos_UAV1 = DPSS_Constants.P_GLOBAL => direction_UAV1 = -1
				and pos_UAV2 = DPSS_Constants.P_GLOBAL => direction_UAV2 = -1
				and pos_UAV3 = DPSS_Constants.P_GLOBAL => direction_UAV3 = -1;
				
			guarantee "Directions are in bounds":
				(	 direction_UAV1 = 1 or direction_UAV1 = -1)
				and (direction_UAV2 = 1 or direction_UAV2 = -1)
				and (direction_UAV3 = 1 or direction_UAV3 = -1);
			
		**};
		
	end DPSS_3_Vehicles_LearnN;

	system implementation DPSS_3_Vehicles_LearnN.Impl
		subcomponents
			UAV1: system UAV_Pkg::UAV;
			UAV2: system UAV_Pkg::UAV;
			UAV3: system UAV_Pkg::UAV;
			
		connections
			
			--Suggested initial directions
			sug_init_dir_UAV1: port	suggested_initial_direction_UAV1 -> UAV1.suggested_initial_direction;
			sug_init_dir_UAV2: port	suggested_initial_direction_UAV2 -> UAV2.suggested_initial_direction;
			sug_init_dir_UAV3: port	suggested_initial_direction_UAV3 -> UAV3.suggested_initial_direction;
			
			--Initial positions
			init_pos_UAV1: port initial_pos_UAV1 -> UAV1.initial_position;
			init_pos_UAV2: port initial_pos_UAV2 -> UAV2.initial_position;
			init_pos_UAV3: port initial_pos_UAV3 -> UAV3.initial_position;
			
			--Share position with right neighbor (get left neighbor's position)
			pos_UAV1_to_UAV2 : port UAV1.pos -> UAV2.pos_LN;
			pos_UAV2_to_UAV3 : port UAV2.pos -> UAV3.pos_LN;

			--Share position with left neighbor (get right neighbor's position) 
			pos_UAV2_to_UAV1 : port UAV2.pos -> UAV1.pos_RN;
			pos_UAV3_to_UAV2 : port UAV3.pos -> UAV2.pos_RN;

			--Share perimRight with left neighbor (get right neighbor's perimRight)
--			perimRight_UAV2_to_UAV1 : port UAV2.perimRight -> UAV1.perimRight_RN;
--			perimRight_UAV3_to_UAV2 : port UAV3.perimRight -> UAV2.perimRight_RN;

			--Share numRight with left neighbor (get right neighbor's numRight)
			numRight_UAV2_to_UAV1 : port UAV2.numRight -> UAV1.numRight_RN;
			numRight_UAV3_to_UAV2 : port UAV3.numRight -> UAV2.numRight_RN;
			
			--Share numLeft with with right neighbor (get left neighbor's numLeft)
			numLeft_UAV1_to_UAV2 : port UAV1.numLeft -> UAV2.numLeft_LN;
			numLeft_UAV2_to_UAV3 : port UAV2.numLeft -> UAV3.numLeft_LN;
			
			--Output positions of the UAVs
			pos_UAV1_output : port UAV1.pos -> pos_UAV1;
			pos_UAV2_output : port UAV2.pos -> pos_UAV2;
			pos_UAV3_output : port UAV3.pos -> pos_UAV3;
						
		annex agree {**

			--Assign direction variables (used at the system level)
			assign direction_UAV1 = UAV1.direction;
			assign direction_UAV2 = UAV2.direction;			
			assign direction_UAV3 = UAV3.direction;
			
			--Assign goal variables (used at the system level)
			assign goal_UAV1 = UAV1.goal;
			assign goal_UAV2 = UAV2.goal;
			assign goal_UAV3 = UAV3.goal;			
			
			--We use assertions since the truth value of the perimeter is not a system input that we can pass in.
			--We could make P_GLOBAL an input, but that would introduce another nonlinearity, so we'll first see if we can prove the theorem when P_GLOBAL is a constant.
			assert UAV1.P_truth = DPSS_Constants.P_GLOBAL;
			assert UAV2.P_truth = DPSS_Constants.P_GLOBAL;
			assert UAV3.P_truth = DPSS_Constants.P_GLOBAL;
						
			--Since the leftmost UAV has no left neighbor, set pos_LN to something to the left of the left endpoint (so UAV1 never meets its "phantom" neighbor).
			assert UAV1.pos_LN = -1.0; 

			--Since the rightmost UAV has no right neighbor, set pos_RN to something to the right of the right endpoint (so UAVN never meets its "phantom" neighbor).
			assert UAV3.pos_RN = DPSS_Constants.P_GLOBAL + 1.0;
			
--			--Since the leftmost UAV has no left neighbor, set numLeft_LN to 0 to satisfy the assumption in the UAV model
--			assert UAV1.numLeft_LN = 0;
--			
--			--Since the rightmost UAV has no right neighbor, set numRight_RN to 0 to satisfy the assumption in the UAV model
--			assert UAV3.numRight_RN = 0;
			
			--Used in lemmas
			eq steps : int = 1 -> pre(steps + 1);
			
			--Expected number of steps it takes to converge to the stable configuration
			--This might be more for unknown N and P
			--This is a guess at this point
			--eq steps_to_converge : int = 22;
			
			----------------------------------------------------------------------------
			--OBSERVERS (Invalid lemmas that produce examples of behavior of interest)
			----------------------------------------------------------------------------
						
			lemma "Observer: Worst time convergence":
				not (
					stable
					--Initially, all UAVs are heading to the left
					and DPSS_Node_Lib.initial_value_true(direction_UAV1 = -1)
					and DPSS_Node_Lib.initial_value_true(direction_UAV2 = -1)
					and DPSS_Node_Lib.initial_value_true(direction_UAV3 = -1)
					--and the UAVs are located close together on the right end of the perimeter (but just shy of the right boundary)
					and DPSS_Node_Lib.initial_value_true(pos_UAV1 = 9.97)
					and DPSS_Node_Lib.initial_value_true(pos_UAV2 = 9.98)
					and DPSS_Node_Lib.initial_value_true(pos_UAV3 = 9.99)
				);
				
--			lemma "Observer: Worst steps convergence (not yet stable after steps_to_converge - 1 steps)":
--				steps = steps_to_converge - 1 => stable;
									
			lemma "Observer: A 6 step trace":
				not (steps > 5 
				and	pos_UAV1 = 0.0
				);
			
			----------------------------------------------------------------------------
			--LEMMAS (We expect these to be true and to potentially help the model checker prove the guarantees)
			----------------------------------------------------------------------------			

			--UAVs stay in bounds (prove one at a time)	
			lemma "UAV1 stays in bounds": 0.0 <= pos_UAV1 and pos_UAV1 <= DPSS_Constants.P_GLOBAL;
			lemma "UAV2 stays in bounds": 0.0 <= pos_UAV2 and pos_UAV2 <= DPSS_Constants.P_GLOBAL;
			lemma "UAV3 stays in bounds": 0.0 <= pos_UAV3 and pos_UAV3 <= DPSS_Constants.P_GLOBAL;		
							
			lemma "Once stable, stays stable":
				true -> pre(stable)	=> stable;
					
--			lemma "Stable on and after steps_to_converge steps":
--				steps >= steps_to_converge => stable;
			
			lemma "Stable on and after 20 steps":
				steps >= 20 => stable;
				
			lemma "Stable on and after 21 steps":
				steps >= 21 => stable;
			
			lemma "Stable on and after 25 steps":
				steps >= 25 => stable;
			
			lemma "Stable implies deltaT = T/N":
				stable => deltaT = DPSS_Constants.Time_to_travel_full_length/N_real;
			
			lemma "deltaT is strictly between 0 and T":
				true -> (
					deltaT > 0.0 
					and deltaT < DPSS_Constants.Time_to_travel_full_length
					);

			-- This is a good way to find the real time bound!	
			lemma "Time to become stable is less than 3T":
				(stable and not (pre(stable))) => 
					(time < 3.0*DPSS_Constants.Time_to_travel_full_length);

			-- This is a good way to find the real time bound!	
			lemma "Time to become stable is less than 4T":
				(stable and not (pre(stable))) => 
					(time < 4.0*DPSS_Constants.Time_to_travel_full_length);
					
			-- This is a good way to find the real time bound!	
			lemma "Time to become stable is less than 5T":
				(stable and not (pre(stable))) => 
					(time < 5.0*DPSS_Constants.Time_to_travel_full_length);
			
		**};
		
	end DPSS_3_Vehicles_LearnN.Impl;

end DPSS_3_Vehicles_LearnN_Pkg;